package rules
import com.example.demo.model.Account
import com.example.demo.model.Bill
import com.example.demo.model.BillStatus
import com.example.demo.model.BillType
import com.example.demo.model.Transaction
import com.example.demo.model.Renewal
import com.example.demo.utils.Utils
import java.util.List


rule "First report"
	agenda-group "first-report"
	no-loop

    when
        $accounts: List()

        accumulate(
        	Account($a: this, passedYears() >= 5, $bills: bills) from $accounts 
        	and Number(intValue > 10) from accumulate(
        		Bill($b: this, passedMonths() <= 18) from $bills,
        		count($b)
        	)
        	and Number(doubleValue > 10000000) from accumulate(
        		Bill(passedMonths() <= 18, $base: base) from $bills,
	        	sum($base)
        	),
        	$result: collectSet($a)
        )

    then
        modify($accounts) {clear(), addAll($result)}

end

rule "Second report"
	agenda-group "second-report"
	no-loop

    when
        $accounts: List()

        accumulate(
        	Account($a: this, $bills: bills) from $accounts 
        	and Number(intValue <= 0) from accumulate(
        		Bill($b: this, status == BillStatus.ABORTED) from $bills,
        		count($b)
        	)
        	and Number(intValue > 10) from accumulate(
        		Bill($b: this, months() > 8, passedMonths() <= 18) from $bills,
	        	count($b)
        	)
        	and Number(doubleValue > 1000000) from accumulate(
        		Bill(passedMonths() <= 12, type != BillType.RSD, $base: Utils.convertToRSD(type, base)) from $bills,
        		sum($base) 
        	),
        	$result: collectSet($a)
        )
        
    then
        modify($accounts) {clear(), addAll($result)}

end

rule "Third report"
	agenda-group "third-report"
	no-loop

    when
        $accounts: List()

        accumulate(
        	Account($a: this, $bills: bills) from $accounts 
			and Number(intValue >= 5) from accumulate(
				Bill($renewals: renewals) from $bills 
				and accumulate(
	        		Renewal($r: this) from $renewals,
	        		$temp: count($r)
	        	),
	        	sum($temp)
			)
			and Number(doubleValue > 6) from accumulate(			
				Bill($renewals: renewals) from $bills 
				and accumulate(
	        		Renewal($amount: amount) from $renewals,
	        		$temp: average($amount)
	        	),
	        	average($temp)
			)
			and Number(doubleValue > 500000) from accumulate(			
				Bill($transactions: transactions) from $bills 
				and accumulate(
	        		Transaction($amount: Utils.convertToRSD(bill.type, amount)) from $transactions,
	        		$temp: average($amount)
	        	),
	        	average($temp)
			),
	        $result: collectSet($a)
        )

    then
        modify($accounts) {clear(), addAll($result)}

end
