package rules
import com.example.demo.model.Bill
import com.example.demo.model.BillStatus
import com.example.demo.model.BillType
import com.example.demo.model.Transaction
import com.example.demo.rules.IncreaseResponse


rule "Passed time less than 5%"
	agenda-group "increase-rules"
	salience 10
	
	when
		Bill(passedTime() < 0.05)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase bill with passed time less than 5%.");}

end

rule "Passed time more than 95%"
	agenda-group "increase-rules"
	salience 10

	when
		Bill(passedTime() > 0.95)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase bill with passed time more than 95%.");}

end

rule "Amount less than 10% of bill balance"
	agenda-group "increase-rules"
	salience 10

	when
        Bill($balance: balance)
        Double(this < 0.1 * $balance)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by less than 10% of bill balance.");}

end

rule "Amount more than 50% of bill balance"
	agenda-group "increase-rules"
	salience 10

	when
        Bill($balance: balance)
        Double(this > 0.5 * $balance)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 50% of bill balance.");}

end

rule "RSD bill - duration less than 6 months"
	agenda-group "increase-rules"
	salience 10

	when
        Bill(type.equals(BillType.RSD), months() < 6)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase RSD bill with duration less than 6 months.");}

end

rule "RSD bill - base less than 75.000 RSD"
	agenda-group "increase-rules"
	salience 10

	when
        Bill(type.equals(BillType.RSD), base < 75000)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase RSD bill with base less than 75.000 RSD.");}

end

rule "RSD bill - 5 more increases"
	agenda-group "increase-rules"
	salience 10

	when
        Bill(type.equals(BillType.RSD), $transactions: transactions)
		$response: IncreaseResponse(!valid, message == null)
        Number(intValue >= 5) from accumulate(
        	Transaction($transaction: this) from $transactions,
        	count($transaction)
        )
	then
		modify($response){setMessage("You can't increase RSD bill that has already been increased at least 5 times.");}

end

rule "Foreign bill - duration less than 4 months"
	agenda-group "increase-rules"
	salience 10

	when
        Bill(!type.equals(BillType.RSD), months() < 4)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase foreign bill with duration less than 4 months.");}

end

rule "Foreign bill - base less than 750 currency units"
	agenda-group "increase-rules"
	salience 10

	when
        Bill(!type.equals(BillType.RSD), base < 750)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase foreign bill with base less than 750 currency units.");}

end

rule "Foreign bill - 3 more increases"
	agenda-group "increase-rules"
	salience 10

	when
        Bill(!type.equals(BillType.RSD), $transactions: transactions)
		$response: IncreaseResponse(!valid, message == null)
        Number(intValue >= 3) from accumulate(
        	Transaction($transaction: this) from $transactions,
        	count($transaction)
        )
	then
		modify($response){setMessage("You can't increase foreign bill that has already been increased at least 3 times.");}

end

rule "Maximum increase amount is 40% of balance pt1"
	agenda-group "increase-rules"
	salience 10

	when
        Bill(
        	type.equals(BillType.RSD) && base > 200000 ||
        	!type.equals(BillType.RSD) && base > 2000,
        	$balance: balance
        )
		Double(this > 0.4 * $balance)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 40% of balance.");}

end

rule "Maximum increase amount is 40% of balance pt2"
	agenda-group "increase-rules"
	salience 10

	when
        Bill($type: type, $balance: balance, $bills: account.bills)
		Double(this > 0.4 * $balance)
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
        	Bill($bill: this, status.equals(BillStatus.ACTIVE), type.equals(BillType.RSD)) from $bills,
        	$cnt1: count($bill)
        )
        accumulate(
        	Bill($bill: this, status.equals(BillStatus.ACTIVE), !type.equals(BillType.RSD)) from $bills,
        	$cnt2: count($bill)
        )
        eval(
        	$type.equals(BillType.RSD) && $cnt1.intValue() > 3 || 
        	!$type.equals(BillType.RSD) && $cnt2.intValue() > 2
        )
	then
		modify($response){setMessage("You can't increase by more than 40% of balance.");}

end

rule "Maximum increase amount is 40% of balance pt3"
	agenda-group "increase-rules"
	salience 10

	when
        Bill($type: type, $base: base, $balance: balance, $transactions: transactions)
		Double(this > 0.4 * $balance)
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
			Transaction($amount: amount) from $transactions,
			$sum1: sum($amount)
        )
        accumulate(
			Transaction($amount: amount) from $transactions,
			$sum2: sum($amount)
        )
        eval(
        	$type.equals(BillType.RSD) && $sum1.doubleValue() > 0.5 * $base || 
        	!$type.equals(BillType.RSD) && $sum2.doubleValue() > 0.3 * $base
        )
	then
		modify($response){setMessage("You can't increase by more than 40% of balance.");}

end

rule "Maximum increase amount is 35% of balance pt1"
	agenda-group "increase-rules"
	salience 10

	when
        Bill(
        	type.equals(BillType.RSD) && base > 150000 || 
        	!type.equals(BillType.RSD) && base > 1500,
        	$balance: balance
        )        
		Double(this > 0.35 * $balance)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 35% of balance.");}

end

rule "Maximum increase amount is 35% of balance pt2"
	agenda-group "increase-rules"
	salience 10

	when
        Bill($type: type, $balance: balance, $bills: account.bills)
		Double(this > 0.35 * $balance)
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
        	Bill($bill: this, status.equals(BillStatus.ACTIVE), type.equals(BillType.RSD)) from $bills,
        	$cnt1: count($bill)
        )
        accumulate(
        	Bill($bill: this, status.equals(BillStatus.ACTIVE), !type.equals(BillType.RSD)) from $bills,
        	$cnt2: count($bill)
        )
        eval(
        	$type.equals(BillType.RSD) && $cnt1.intValue() > 4 || 
        	!$type.equals(BillType.RSD) && $cnt2.intValue() > 3)
	then
		modify($response){setMessage("You can't increase by more than 35% of balance.");}

end

rule "Maximum increase amount is 35% of balance pt3"
	agenda-group "increase-rules"
	salience 10

	when
        Bill($type: type, $base: base, $balance: balance, $transactions: transactions)
		Double(this > 0.35 * $balance)
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
			Transaction($amount: amount) from $transactions,
			$sum1: sum($amount)
        )
        accumulate(
			Transaction($amount: amount) from $transactions,
			$sum2: sum($amount)
        )
        eval(
        	$type.equals(BillType.RSD) && $sum1.doubleValue() > 0.6 * $base || 
        	!$type.equals(BillType.RSD) && $sum2.doubleValue() > 0.4 * $base
        )
	then
		modify($response){setMessage("You can't increase by more than 35% of balance.");}

end

rule "Maximum increase amount is 30% of balance pt1"
	agenda-group "increase-rules"
	salience 10

	when
        Bill(
        	type.equals(BillType.RSD) && base > 100000 || 
        	!type.equals(BillType.RSD) && base > 1000,
        	$balance: balance
        )
		Double(this > 0.3 * $balance)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 30% of balance.");}

end

rule "Maximum increase amount is 30% of balance pt2"
	agenda-group "increase-rules"
	salience 10

	when
        Bill($type: type, $balance: balance, $bills: account.bills)
		Double(this > 0.3 * $balance)
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
        	Bill($bill: this, status.equals(BillStatus.ACTIVE), type.equals(BillType.RSD)) from $bills,
        	$cnt1: count($bill)
        )
		accumulate(
        	Bill($bill: this, status.equals(BillStatus.ACTIVE), !type.equals(BillType.RSD)) from $bills,
        	$cnt2: count($bill)
        )
        eval(
        	$type.equals(BillType.RSD) && $cnt1.intValue() > 5 || 
        	!$type.equals(BillType.RSD) && $cnt2.intValue() > 4
        )
	then
		modify($response){setMessage("You can't increase by more than 30% of balance.");}

end

rule "Maximum increase amount is 30% of balance pt3"
	agenda-group "increase-rules"
	salience 10

	when
        Bill($type: type, $base: base, $balance: balance, $transactions: transactions)
		Double(this > 0.3 * $balance)
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
			Transaction($amount: amount) from $transactions,
			$sum1: sum($amount)
        )
        accumulate(
			Transaction($amount: amount) from $transactions,
			$sum2: sum($amount)
        )
        eval(
        	$type.equals(BillType.RSD) && $sum1.doubleValue() > 0.7 * $base ||
        	!$type.equals(BillType.RSD) && $sum2.doubleValue() > 0.5 * $base
        )
	then
		modify($response){setMessage("You can't increase by more than 30% of balance.");}

end

rule "Valid params"
	agenda-group "increase-rules"
	salience 1

    when
		$response: IncreaseResponse(!valid, message == null)
    then    
		modify($response){setValid(true);}

end

