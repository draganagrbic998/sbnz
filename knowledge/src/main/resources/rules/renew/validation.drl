package rules
import com.example.demo.model.Bill
import com.example.demo.model.BillType
import com.example.demo.model.Renewal
import com.example.demo.rules.RenewalResponse


rule "Amount less than 3 months"
	agenda-group "renew-rules"
	salience 17

    when
		Integer(this < 3)
		$response: RenewalResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't renew bill for less than 3 months.");}

end

rule "Amount more than 24 months"
	agenda-group "renew-rules"
	salience 16

    when
		Integer(this > 24)
		$response: RenewalResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't renew bill for more than 24 months.");}

end

rule "RSD bill - duration of 4 months"
	agenda-group "renew-rules"
	salience 15

    when
        Bill(type == BillType.RSD, months() <= 4)
		$response: RenewalResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't renew RSD bill with duration of 4 months.");}

end

rule "RSD bill - duration of at least 42 months"
	agenda-group "renew-rules"
	salience 14

    when
        Bill(type == BillType.RSD, months() >= 42)
		$response: RenewalResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't renew RSD bill with duration of at least 42 months.");}

end

rule "RSD bill - base less than 75.000 RSD"
	agenda-group "renew-rules"
	salience 13

	when
        Bill(type == BillType.RSD, base < 75000)
		$response: RenewalResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't renew RSD bill with base less than 75.000 RSD.");}

end

rule "Foreign bill - duration of 2 months"
	agenda-group "renew-rules"
	salience 12

    when
        Bill(type != BillType.RSD, months() <= 2)
		$response: RenewalResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't renew foreign bill with duration of 2 months.");}

end

rule "Foreign bill - duration of at least 36 months"
	agenda-group "renew-rules"
	salience 11

    when
        Bill(type != BillType.RSD, months() >= 36)
		$response: RenewalResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't renew foreign bill with duration of at least 36 months.");}

end

rule "Foreign bill - base less than 750 currency units"
	agenda-group "renew-rules"
	salience 10

	when
        Bill(type != BillType.RSD, base < 750)
		$response: RenewalResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't renew foreign bill with base less than 750 currency units.");}

end

rule "Maximum renewal amount is 18 months pt1"
	agenda-group "renew-rules"
	salience 9

	when
        Bill(
        	type == BillType.RSD && base < 250000 ||
        	type != BillType.RSD && base < 2500
        )
        Integer(this > 18)
		$response: RenewalResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't renew bill for more than 18 months.");}

end

rule "Maximum renewal amount is 18 months pt2"
	agenda-group "renew-rules"
	salience 8

	when
		Bill($renewals: renewals)
        Integer(this > 18)
		$response: RenewalResponse(!valid, message == null)
        Number(intValue >= 1) from accumulate(
        	Renewal($r: this) from $renewals,
        	count($r)
        )
	then
		modify($response){setMessage("You can't renew bill for more than 18 months.");}

end

rule "Maximum renewal amount is 12 months pt1"
	agenda-group "renew-rules"
	salience 7

	when
        Bill(
        	type == BillType.RSD && base < 200000 ||
        	type != BillType.RSD && base < 2000
        )
        Integer(this > 12)
		$response: RenewalResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't renew bill for more than 12 months.");}

end

rule "Maximum renewal amount is 12 months pt2"
	agenda-group "renew-rules"
	salience 6

	when
		Bill($renewals: renewals)
        Integer(this > 12)
		$response: RenewalResponse(!valid, message == null)
        Number(intValue >= 2) from accumulate(
        	Renewal($r: this) from $renewals,
        	count($r)
        )
	then
		modify($response){setMessage("You can't renew bill for more than 12 months.");}

end

rule "Maximum renewal amount is 9 months pt1"
	agenda-group "renew-rules"
	salience 5

	when
        Bill(
        	type == BillType.RSD && base < 150000 ||
        	type != BillType.RSD && base < 1500
        )
        Integer(this > 9)
		$response: RenewalResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't renew bill for more than 9 months.");}

end

rule "Maximum renewal amount is 9 months pt2"
	agenda-group "renew-rules"
	salience 4

	when
		Bill($renewals: renewals)
        Integer(this > 9)
		$response: RenewalResponse(!valid, message == null)
        Number(intValue >= 3) from accumulate(
        	Renewal($r: this) from $renewals,
        	count($r)
        )
	then
		modify($response){setMessage("You can't renew bill for more than 9 months.");}

end

rule "Maximum renewal amount is 6 months pt1"
	agenda-group "renew-rules"
	salience 3

	when
        Bill(
        	type == BillType.RSD && base < 100000 ||
        	type != BillType.RSD && base < 1000
        )
        Integer(this > 6)
		$response: RenewalResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't renew bill for more than 6 months.");}

end

rule "Maximum renewal amount is 6 months pt2"
	agenda-group "renew-rules"
	salience 2

	when
		Bill($renewals: renewals)
        Integer(this > 6)
		$response: RenewalResponse(!valid, message == null)
        Number(intValue >= 4) from accumulate(
        	Renewal($r: this) from $renewals,
        	count($r)
        )
	then
		modify($response){setMessage("You can't renew bill for more than 6 months.");}

end

rule "Valid params"
	agenda-group "renew-rules"
	salience 1

    when
		$response: RenewalResponse(!valid, message == null)
    then    
		modify($response){setValid(true)}

end

