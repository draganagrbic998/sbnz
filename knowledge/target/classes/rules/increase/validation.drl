package rules
import com.example.demo.model.Bill
import com.example.demo.model.BillStatus
import com.example.demo.model.BillType
import com.example.demo.model.Transaction
import com.example.demo.rules.IncreaseResponse


rule "Passed time more than 95%"
	agenda-group "increase-rules"

	when
		Bill(passedTime() > 0.95)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase bill with passed time more than 95%.");}

end

rule "Amount less than 10% of balance"
	agenda-group "increase-rules"

	when
        Bill($balance: balance)
        Double(this < 0.1 * $balance)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by less than 10% of bill balance.");}

end

rule "Amount more than 50% of balance"
	agenda-group "increase-rules"

	when
        Bill($balance: balance)
        Double(this > 0.5 * $balance)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 50% of bill balance.");}

end

rule "RSD bill - duration less than 6 months"
	agenda-group "increase-rules"

	when
        Bill(type == BillType.RSD, months() < 6)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase RSD bill with duration less than 6 months.");}

end

rule "RSD bill - base less than 75.000 RSD"
	agenda-group "increase-rules"

	when
        Bill(type == BillType.RSD, base < 75000)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase RSD bill with base less than 75.000 RSD.");}

end

rule "RSD bill - 5 more increases"
	agenda-group "increase-rules"

	when
        Bill(type == BillType.RSD, $transactions: transactions)
        Number(intValue >= 5) from accumulate(
        	Transaction($t: this) from $transactions,
        	count($t)
        )
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase RSD bill that has already been increased at least 5 times.");}

end

rule "Foreign bill - duration less than 4 months"
	agenda-group "increase-rules"

	when
        Bill(type != BillType.RSD, months() < 4)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase foreign bill with duration less than 4 months.");}

end

rule "Foreign bill - base less than 750 currency units"
	agenda-group "increase-rules"

	when
        Bill(type != BillType.RSD, base < 750)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase foreign bill with base less than 750 currency units.");}

end

rule "Foreign bill - 3 more increases"
	agenda-group "increase-rules"

	when
        Bill(type != BillType.RSD, $transactions: transactions)
        Number(intValue >= 3) from accumulate(
        	Transaction($t: this) from $transactions,
        	count($t)
        )
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase foreign bill that has already been increased at least 3 times.");}

end

rule "Maximum increase amount is 40% of balance pt1"
	agenda-group "increase-rules"

	when
        Bill(
        	type == BillType.RSD && base > 200000 ||
        	type != BillType.RSD && base > 2000,
        	$balance: balance
        )
		Double(this > 0.4 * $balance)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 40% of balance.");}

end

rule "Maximum increase amount is 40% of balance pt2"
	agenda-group "increase-rules"

	when
        Bill($type: type, $balance: balance, $bills: account.bills)
		Double(this > 0.4 * $balance)
        accumulate(
        	Bill($b: this, status == BillStatus.ACTIVE, sameType($type)) from $bills,
        	$cnt: count($b)
        )
        eval(
        	$type == BillType.RSD && $cnt > 3 || 
        	$type != BillType.RSD && $cnt > 2
        )
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 40% of balance.");}

end

rule "Maximum increase amount is 40% of balance pt3"
	agenda-group "increase-rules"

	when
        Bill($type: type, $base: base, $balance: balance, $transactions: transactions)
		Double(this > 0.4 * $balance)
        accumulate(
			Transaction($amount: amount) from $transactions,
			$sum: sum($amount)
        )
        eval(
        	$type == BillType.RSD && $sum > 0.5 * $base || 
        	$type != BillType.RSD && $sum > 0.3 * $base
        )
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 40% of balance.");}

end

rule "Maximum increase amount is 35% of balance pt1"
	agenda-group "increase-rules"

	when
        Bill(
        	type == BillType.RSD && base > 150000 || 
        	type != BillType.RSD && base > 1500,
        	$balance: balance
        )        
		Double(this > 0.35 * $balance)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 35% of balance.");}

end

rule "Maximum increase amount is 35% of balance pt2"
	agenda-group "increase-rules"

	when
        Bill($type: type, $balance: balance, $bills: account.bills)
		Double(this > 0.35 * $balance)
        accumulate(
        	Bill($b: this, status == BillStatus.ACTIVE, sameType($type)) from $bills,
        	$cnt: count($b)
        )
        eval(
        	$type == BillType.RSD && $cnt > 4 || 
        	$type != BillType.RSD && $cnt > 3
        )
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 35% of balance.");}

end

rule "Maximum increase amount is 35% of balance pt3"
	agenda-group "increase-rules"

	when
        Bill($type: type, $base: base, $balance: balance, $transactions: transactions)
		Double(this > 0.35 * $balance)
        accumulate(
			Transaction($amount: amount) from $transactions,
			$sum: sum($amount)
        )
        eval(
        	$type == BillType.RSD && $sum > 0.6 * $base || 
        	$type != BillType.RSD && $sum > 0.4 * $base
        )
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 35% of balance.");}

end

rule "Maximum increase amount is 30% of balance pt1"
	agenda-group "increase-rules"

	when
        Bill(
        	type == BillType.RSD && base > 100000 || 
        	type != BillType.RSD && base > 1000,
        	$balance: balance
        )
		Double(this > 0.3 * $balance)
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 30% of balance.");}

end

rule "Maximum increase amount is 30% of balance pt2"
	agenda-group "increase-rules"

	when
        Bill($type: type, $balance: balance, $bills: account.bills)
		Double(this > 0.3 * $balance)
        accumulate(
        	Bill($b: this, status == BillStatus.ACTIVE, sameType($type)) from $bills,
        	$cnt: count($b)
        )
        eval(
        	$type == BillType.RSD && $cnt > 5 || 
        	$type != BillType.RSD && $cnt > 4
        )
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 30% of balance.");}

end

rule "Maximum increase amount is 30% of balance pt3"
	agenda-group "increase-rules"

	when
        Bill($type: type, $base: base, $balance: balance, $transactions: transactions)
		Double(this > 0.3 * $balance)
        accumulate(
			Transaction($amount: amount) from $transactions,
			$sum: sum($amount)
        )
        eval(
        	$type == BillType.RSD && $sum > 0.7 * $base ||
        	$type != BillType.RSD && $sum > 0.5 * $base
        )
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't increase by more than 30% of balance.");}

end

rule "Valid params"
	agenda-group "increase-rules"

    when
		$response: IncreaseResponse(!valid, message == null)
    then    
		modify($response){setValid(true);}

end

