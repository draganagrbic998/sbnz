package rules

import com.example.demo.model.Transaction
import com.example.demo.model.TransactionType
import com.example.demo.model.Bill
import com.example.demo.model.BillType
import com.example.demo.model.BillStatus
import com.example.demo.rules.IncreaseResponse


rule "Less than 5% of bill period passed"
	agenda-group "increase-rules"

    when
		Bill(passedTime() < 0.05)
		$response: IncreaseResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't deposit on a bill with less than 5% of period passed.");}

end

rule "More than 95% of bill period passed"
	agenda-group "increase-rules"

    when
		Bill(passedTime() > 0.95)
		$response: IncreaseResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't deposit on a bill with more than 95% of period passed.");}

end

rule "Amount lower than 10% of bill balance"
	agenda-group "increase-rules"

    when
        Bill($balance: balance)
        Number(this / $balance < 0.1)
		$response: IncreaseResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't deposit less than 10% of the bill balance.");}

end

rule "Amount higher than 50% of bill balance"
	agenda-group "increase-rules"

    when
        Bill($balance: balance)
        Number(this / $balance > 0.5)
		$response: IncreaseResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't deposit more than 50% of the bill balance.");}

end

rule "RSD bill - duration shorter than 6 months"
	agenda-group "increase-rules"

    when
        Bill(type.equals(BillType.RSD), months() < 6)
		$response: IncreaseResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't deposit on a RSD bill with duration shorter than 6 months.");}
		
end

rule "RSD bill - base lower than 75.000 RSD"
	agenda-group "increase-rules"

    when
        Bill(type.equals(BillType.RSD), base < 75000)
		$response: IncreaseResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't deposit on a RSD bill with base lower than 75.000 RSD.");}

end

rule "RSD bill - number of deposits higher than 4"
	agenda-group "increase-rules"

    when
        Bill(type.equals(BillType.RSD), $transactions: transactions)
        Number(intValue > 4) from accumulate(
        	Transaction($transaction: this, type.equals(TransactionType.INCREASE)) from $transactions,
        	count($transaction)
        )
		$response: IncreaseResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't deposit on a RSD bill on which you made a deposit more than 4 times.");}

end

rule "Foreign bill - duration shorter than 4 months"
	agenda-group "increase-rules"

    when
        Bill(!type.equals(BillType.RSD), months() < 4)
		$response: IncreaseResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't deposit on a foreign bill with duration shorter than 4 months.");}
		
end

rule "Foreign bill - base lower than 750 units"
	agenda-group "increase-rules"

    when
        Bill(!type.equals(BillType.RSD), base < 750)
		$response: IncreaseResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't deposit on a foreign bill with base lower than 750 units.");}

end

rule "Foreign bill - number of deposits higher than 2"
	agenda-group "increase-rules"

    when
        Bill(!type.equals(BillType.RSD), $transactions: transactions)
        Number(intValue > 2) from accumulate(
        	Transaction($transaction: this, type.equals(TransactionType.INCREASE)) from $transactions,
        	count($transaction)
        )
		$response: IncreaseResponse(!valid, message == null)
    then  
		modify($response){setMessage("You can't deposit on a foreign bill on which you made a deposit more than 2 times.");}

end

rule "Maximum deposit amount is 40% of balance pt1"
	agenda-group "increase-rules"

	when
        Bill(
        	type.equals(BillType.RSD) && base > 200000 ||
        	!type.equals(BillType.RSD) && base > 2000,
        	$balance: balance
        )
        Number(this / $balance > 0.4)        
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't deposit more than 40% of current bill balance.");}

end

rule "Maximum deposit amount is 40% of balance pt2"
	agenda-group "increase-rules"

	when
	    Bill(type.equals(BillType.RSD), $base: base, $balance: balance, $transactions: transactions, $bills: account.bills)
        Number(this / $balance > 0.4)        
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
        	Bill($bill: this, type.equals(BillType.RSD), status.equals(BillStatus.ACTIVE)) from $bills,
        	$cnt: count($bill)
        )
        accumulate(
         	Transaction(type.equals(TransactionType.INCREASE), $amount: amount) from $transactions,
         	$sum: sum($amount)
        )
       	eval(
			$cnt.intValue() > 3 || $sum.doubleValue() / $base > 0.3
		);
	then
		modify($response){setMessage("You can't deposit more than 40% of current bill balance.");}

end

rule "Maximum deposit amount is 40% of balance pt3"
	agenda-group "increase-rules"

	when
	    Bill(!type.equals(BillType.RSD), $base: base, $balance: balance, $transactions: transactions, $bills: account.bills)
        Number(this / $balance > 0.4)        
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
        	Bill($bill: this, !type.equals(BillType.RSD), status.equals(BillStatus.ACTIVE)) from $bills,
        	$cnt: count($bill)
        )
        accumulate(
         	Transaction(type.equals(TransactionType.INCREASE), $amount: amount) from $transactions,
         	$sum: sum($amount)
        )
       	eval(
			$cnt.intValue() > 2 || $sum.doubleValue() / $base > 0.25
		);
	then
		modify($response){setMessage("You can't deposit more than 40% of current bill balance.");}

end

rule "Maximum deposit amount is 35% of balance pt1"
	agenda-group "increase-rules"

	when
        Bill(
        	type.equals(BillType.RSD) && base > 150000 ||
        	!type.equals(BillType.RSD) && base > 1500,
        	$balance: balance
        )
        Number(this / $balance > 0.35)        
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't deposit more than 35% of current bill balance.");}

end

rule "Maximum deposit amount is 35% of balance pt2"
	agenda-group "increase-rules"

	when
	    Bill(type.equals(BillType.RSD), $base: base, $balance: balance, $transactions: transactions, $bills: account.bills)
        Number(this / $balance > 0.35)        
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
        	Bill($bill: this, type.equals(BillType.RSD), status.equals(BillStatus.ACTIVE)) from $bills,
        	$cnt: count($bill)
        )
        accumulate(
         	Transaction(type.equals(TransactionType.INCREASE), $amount: amount) from $transactions,
         	$sum: sum($amount)
        )
       	eval(
			$cnt.intValue() > 4 || $sum.doubleValue() / $base > 0.4
		);
	then
		modify($response){setMessage("You can't deposit more than 35% of current bill balance.");}

end

rule "Maximum deposit amount is 35% of balance pt3"
	agenda-group "increase-rules"

	when
	    Bill(!type.equals(BillType.RSD), $base: base, $balance: balance, $transactions: transactions, $bills: account.bills)
        Number(this / $balance > 0.35)        
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
        	Bill($bill: this, !type.equals(BillType.RSD), status.equals(BillStatus.ACTIVE)) from $bills,
        	$cnt: count($bill)
        )
        accumulate(
         	Transaction(type.equals(TransactionType.INCREASE), $amount: amount) from $transactions,
         	$sum: sum($amount)
        )
       	eval(
			$cnt.intValue() > 3 || $sum.doubleValue() / $base > 0.3
		);
	then
		modify($response){setMessage("You can't deposit more than 35% of current bill balance.");}

end

rule "Maximum deposit amount is 30% of balance pt1"
	agenda-group "increase-rules"

	when
        Bill(
        	type.equals(BillType.RSD) && base > 100000 ||
        	!type.equals(BillType.RSD) && base > 1000,
        	$balance: balance
        )
        Number(this / $balance > 0.3)        
		$response: IncreaseResponse(!valid, message == null)
	then
		modify($response){setMessage("You can't deposit more than 30% of current bill balance.");}

end

rule "Maximum deposit amount is 30% of balance pt2"
	agenda-group "increase-rules"

	when
	    Bill(type.equals(BillType.RSD), $base: base, $balance: balance, $transactions: transactions, $bills: account.bills)
        Number(this / $balance > 0.3)        
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
        	Bill($bill: this, type.equals(BillType.RSD), status.equals(BillStatus.ACTIVE)) from $bills,
        	$cnt: count($bill)
        )
        accumulate(
         	Transaction(type.equals(TransactionType.INCREASE), $amount: amount) from $transactions,
         	$sum: sum($amount)
        )
       	eval(
			$cnt.intValue() > 5 || $sum.doubleValue() / $base > 0.5
		);
	then
		modify($response){setMessage("You can't deposit more than 30% of current bill balance.");}

end

rule "Maximum deposit amount is 30% of balance pt3"
	agenda-group "increase-rules"

	when
	    Bill(!type.equals(BillType.RSD), $base: base, $balance: balance, $transactions: transactions, $bills: account.bills)
        Number(this / $balance > 0.3)        
		$response: IncreaseResponse(!valid, message == null)
        accumulate(
        	Bill($bill: this, !type.equals(BillType.RSD), status.equals(BillStatus.ACTIVE)) from $bills,
        	$cnt: count($bill)
        )
        accumulate(
         	Transaction(type.equals(TransactionType.INCREASE), $amount: amount) from $transactions,
         	$sum: sum($amount)
        )
       	eval(
			$cnt.intValue() > 4 || $sum.doubleValue() / $base > 0.4
		);
	then
		modify($response){setMessage("You can't deposit more than 30% of current bill balance.");}

end

rule "Valid params"
	agenda-group "increase-rules"

    when
		$response: IncreaseResponse(!valid, message == null)
    then    
		modify($response){setValid(true)}

end

