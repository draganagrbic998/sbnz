package rules
import com.example.demo.model.Bill
import com.example.demo.model.BillStatus
import com.example.demo.model.BillType
import com.example.demo.rules.CloseResponse


rule "Passed time more than 80%"
	agenda-group "close-rules"
	salience 8

    when
        Bill(passedTime() > 0.8)
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close bill with passed time more than 80%.");}

end

rule "RSD bill - base less than 20.000 RSD"
	agenda-group "close-rules"
	salience 7

    when
        Bill(type == BillType.RSD, base < 20000)
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close RSD bill with base less than 20.000 RSD.");}

end

rule "The only active RSD bill"
	agenda-group "close-rules"
	salience 6

    when
        Bill(type == BillType.RSD, $bills: account.bills)
        Number(intValue <= 1) from accumulate(
        	Bill($b: this, status == BillStatus.ACTIVE, type == BillType.RSD) from $bills,
        	count($b)
        )
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close the only active RSD bill.");}

end

rule "6 more closed RSD bills"
	agenda-group "close-rules"
	salience 5

    when
        Bill(type == BillType.RSD, $bills: account.bills)
        Number(intValue >= 6) from accumulate(
        	Bill($b: this, status == BillStatus.ABORTED, type == BillType.RSD) from $bills,
        	count($b)
        )
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close more than 6 RSD bills.");}

end

rule "Foreign bill - base less than 200 units"
	agenda-group "close-rules"
	salience 4

    when
        Bill(type != BillType.RSD, base < 200)
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close foreign bill with base less than 200 currency units.");}

end

rule "The only active foreign bill"
	agenda-group "close-rules"
	salience 3

    when
        Bill(type != BillType.RSD, $bills: account.bills)
        Number(intValue <= 1) from accumulate(
        	Bill($b: this, status == BillStatus.ACTIVE, type != BillType.RSD) from $bills,
        	count($b)
        )
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close the only active foreign bill.");}

end

rule "4 more closed foreign bills"
	agenda-group "close-rules"
	salience 2

    when
        Bill(type != BillType.RSD, $bills: account.bills)
        Number(intValue >= 4) from accumulate(
        	Bill($b: this, status == BillStatus.ABORTED, type != BillType.RSD) from $bills,
        	count($b)
        )
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close more than 4 foreign bills.");}

end

rule "Valid params"
	agenda-group "close-rules"
	salience 1

	when
        $response: CloseResponse(!valid, message == null)
	then
		modify($response){setValid(true);}

end