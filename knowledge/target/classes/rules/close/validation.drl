package rules
import com.example.demo.model.Bill
import com.example.demo.model.BillStatus
import com.example.demo.model.BillType
import com.example.demo.rules.CloseResponse


rule "Passed time less than 20%"
	agenda-group "close-rules"
	salience 10

    when
        Bill(passedTime() < 0.2)
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close bill with passed time less than 20%.");}

end

rule "Passed time more than 80%"
	agenda-group "close-rules"
	salience 10

    when
        Bill(passedTime() > 0.8)
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close bill with passed time more than 80%.");}

end

rule "RSD bill - base less than 20.000 RSD"
	agenda-group "close-rules"
	salience 10

    when
        Bill(type.equals(BillType.RSD), base < 20000)
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close RSD bill with base less than 20.000 RSD.");}

end

rule "The only active RSD bill"
	agenda-group "close-rules"
	salience 10

    when
        Bill(type.equals(BillType.RSD), $bills: account.bills)
        Number(intValue <= 1) from accumulate(
        	Bill($bill: this, status.equals(BillStatus.ACTIVE), type.equals(BillType.RSD)) from $bills,
        	count($bill)
        )
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close the only active RSD bill.");}

end

rule "6 more closed RSD bills"
	agenda-group "close-rules"
	salience 10

    when
        Bill(type.equals(BillType.RSD), $bills: account.bills)
        Number(intValue >= 6) from accumulate(
        	Bill($bill: this, status.equals(BillStatus.ABORTED), type.equals(BillType.RSD)) from $bills,
        	count($bill)
        )
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close more than 6 RSD bills.");}

end

rule "Foreign bill - base less than 200 units"
	agenda-group "close-rules"
	salience 10

    when
        Bill(!type.equals(BillType.RSD), base < 200)
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close foreign bill with base less than 200 currency units.");}

end

rule "The only active foreign bill"
	agenda-group "close-rules"
	salience 10

    when
        Bill(!type.equals(BillType.RSD), $bills: account.bills)
        Number(intValue <= 1) from accumulate(
        	Bill($bill: this, status.equals(BillStatus.ACTIVE), !type.equals(BillType.RSD)) from $bills,
        	count($bill)
        )
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close the only active foreign bill.");}

end

rule "4 more closed foreign bills"
	agenda-group "close-rules"
	salience 10

    when
        Bill(!type.equals(BillType.RSD), $bills: account.bills)
        Number(intValue >= 4) from accumulate(
        	Bill($bill: this, status.equals(BillStatus.ABORTED), !type.equals(BillType.RSD)) from $bills,
        	count($bill)
        )
        $response: CloseResponse(!valid, message == null)
    then
        modify($response){setMessage("You can't close more than 4 foreign bills.");}

end

rule "Valid params"
	agenda-group "close-rules"
	salience 1

	when
        $response: CloseResponse(!valid, message == null)
	then
		modify($response){setValid(true);}

end